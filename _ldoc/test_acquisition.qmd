## Testing

```{.julia}
@testset "Test Hgx_αLCB" begin
    Zk, y = test_setup2d((x,y)->x^2+y)
    gp = GPPContext(KernelSE{2}(0.5), 1e-8, Zk, y)
    z = [0.47; 0.47]
    dz = randn(2)
    λ = 2.3
    g(s)  = Hgx_αLCB(gp, z+s*dz, λ)[1]
    dg(s) = Hgx_αLCB(gp, z+s*dz, λ)[2]
    Hg(s) = Hgx_αLCB(gp, z+s*dz, λ)[3]
    @test dg(0)'*dz ≈ diff_fd(g) rtol=1e-6
    @test Hg(0)*dz ≈ diff_fd(dg) rtol=1e-6
end

@testset "Test DψNLG0" begin
    z = 0.123
    @test DψNLG0(z)[2] ≈ diff_fd(z->DψNLG0(z)[1], z) rtol=1e-6
    @test DψNLG0(z)[3] ≈ diff_fd(z->DψNLG0(z)[2], z) rtol=1e-6    
end

@testset "Test DψNLG1" begin
    z = 0.123
    z1 = 30.456
    @test DψNLG0(z)[1] ≈ DψNLG1(z)[1]
    @test DψNLG0(z)[2] ≈ DψNLG1(z)[2]
    @test DψNLG0(z)[3] ≈ DψNLG1(z)[3]
    @test DψNLG1(z)[2] ≈ diff_fd(z->DψNLG1(z)[1], z) rtol=1e-6
    @test DψNLG1(z)[3] ≈ diff_fd(z->DψNLG1(z)[2], z) rtol=1e-6
    @test DψNLG1(z1)[2] ≈ diff_fd(z->DψNLG1(z)[1], z1, h=1e-4) rtol=1e-6
    @test DψNLG1(z1)[3] ≈ diff_fd(z->DψNLG1(z)[2], z1, h=1e-4) rtol=1e-6    
end

@testset "Test DψNLG2" begin
    z = 5.23
    @test DψNLG0(z)[1] ≈ DψNLG2(z)[1]
    @test DψNLG0(z)[2] ≈ DψNLG2(z)[2]
    @test DψNLG0(z)[3] ≈ DψNLG2(z)[3]    
end

@testset begin
    function check_derivs3_NLEI(f)
        Zk, y = test_setup2d((x,y)->x^2+y)
        gp = GPPContext(KernelSE{2}(0.5), 1e-8, Zk, y)
        z = [0.47; 0.47]
        dz = randn(2)
        fopt = -0.1
        g(s)  = f(gp, z+s*dz, fopt)[1]
        dg(s) = f(gp, z+s*dz, fopt)[2]
        Hg(s) = f(gp, z+s*dz, fopt)[3]
        @test dg(0)'*dz ≈ diff_fd(g) rtol=1e-6
        @test Hg(0)*dz ≈ diff_fd(dg) rtol=1e-6
    end
    @testset "Test Hgx_u"      check_derivs3_NLEI(Hgx_u)
    @testset "Test Hgx_ψNLG"   check_derivs3_NLEI(Hgx_ψNLG)
    @testset "Test Hgx_αNLEI0" check_derivs3_NLEI(Hgx_αNLEI0)
    @testset "Test Hgx_αNLEI"  check_derivs3_NLEI(Hgx_αNLEI)
end
```


